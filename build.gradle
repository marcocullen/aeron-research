buildscript {
    ext {
        SBE_VERSION = '1.34.1'
        AERON_VERSION = '1.47.2'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'  // Add shadow plugin
}

def generatedDir = layout.buildDirectory.dir('generated-src/sbe')

group = 'org.starquake'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "uk.co.real-logic:sbe-tool:${SBE_VERSION}"
    implementation "io.aeron:aeron-client:${AERON_VERSION}"
    implementation "io.aeron:aeron-driver:${AERON_VERSION}"
    implementation "io.aeron:aeron-agent:${AERON_VERSION}"
    implementation 'org.agrona:agrona:2.0.1'
    implementation 'org.hdrhistogram:HdrHistogram:2.1.12'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

tasks.register('generateMessages', JavaExec) {
    description = 'Generates SBE message classes'
    group = 'build'

    inputs.files('src/main/resources/messages.xml', 'src/main/resources/sbe/sbe.xsd')
    outputs.dir(generatedDir)

    classpath = sourceSets.main.compileClasspath
    mainClass = 'uk.co.real_logic.sbe.SbeTool'

    systemProperties = [
            'sbe.output.dir': generatedDir.get().asFile.absolutePath,
            'sbe.target.language': 'Java',
            'sbe.validation.stop.on.error': 'true',
            'sbe.validation.xsd': 'src/main/resources/sbe/sbe.xsd'
    ]

    args = ['src/main/resources/messages.xml']
}

sourceSets {
    main {
        java {
            srcDirs += generatedDir
        }
    }
}

tasks.named('compileJava') {
    dependsOn tasks.named('generateMessages')
}

tasks.build.dependsOn tasks.shadowJar

test {
    useJUnitPlatform()
}

class DeploymentConfig {
    String hostname = "set-target-hostname-please"
    String username = "set-a-user"
    String targetDir = "/var/tmp"
}

extensions.create("deployConfig", DeploymentConfig)

tasks.register('packageForDeployment', Tar) {
    dependsOn shadowJar // Ensure the fat jar is built
    compression = Compression.GZIP
    archiveExtension = "tgz"

    from("$projectDir") {
        include "scripts/**"
        include "agent-jar/**"
    }

    from("$buildDir") {
        include "libs/**"
    }

    destinationDirectory = file("$buildDir/dist")
    archiveFileName = "deployment-bundle.tgz"
}

tasks.register('scpDeploy', Exec) {
    dependsOn packageForDeployment

    doFirst {
        def config = project.deployConfig
        def host = project.findProperty('targetHost') ?: config.hostname
        def user = project.findProperty('user') ?: config.username

        commandLine 'sh', '-c', """
            scp -o StrictHostKeyChecking=no \
                ${packageForDeployment.outputs.files.singleFile} \
                ${user}@${host}:${config.targetDir}/
        """

        println "Deploying to ${host}..."
    }

    doLast {
        println "Deployment bundle transferred successfully!"
    }
}